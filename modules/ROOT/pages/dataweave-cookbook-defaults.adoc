= Methods for Settings Default Values
ifndef::env-site,env-github[]
include::_attributes.adoc[]
endif::[]
:keywords: studio, anypoint, transform, transformer, format, aggregate, rename, split, filter convert, xml, json, csv, pojo, java object, metadata, dataweave, data weave, datamapper, dwl, dfl, dw, output structure, input structure, map, mapping, examples

Use one of the following methods to set default values when a payload value is absent or when the value is `null`:

* Using the `default` keyword
* Setting the default in an `if-else` or `else-if` statement
* Using `else` when pattern matching

//LINK TO DW 1.0 EXAMPLES:
include::partial$dataweave1-links.adoc[tag=dataweave1Examples]

== Example of Using the Keyword default to Set a Default Value

Consider a Mule application that expects a JSON input with fields `id` and `name` to do a transformation.
You can configure default values for these fields in case the fields are not present, or their value is `null`. For example:

[source,DataWeave, linenums]
----
%dw 2.0
output application/json
---
{
  "userId": payload.id default "0000",
  "userName": payload.name default "Undefined"
}
----

If the Mule application receives a JSON message with values set for the `id` and `name` fields, then the DataWeave  transformation matches field `id` to `userId` and field `name` to `userName`.

Example: Input JSON
[source,json, linenums]
----
{
  "id": "123",
  "name": "Max the Mule"
}
----

Example: Transformation Output
[source,JSON,linenums]
----
{
  "userId": "123",
  "userName": "Max the Mule"
}
----

However, if the Mule application receives a JSON message without the expected fields, or the fields have `null` values, then the transformation uses the configured default values for fields `userId` and `userName`.

Example: Input JSON
[source,json, linenums]
----
{
  "id": null
}
----

Example: Transformation Output
[source,JSON,linenums]
----
{
  "userId": "0000",
  "userName": "Undefined"
}
----

== Example of Using if-else and else-if Statements to Set Default Values

Another method for providing a default value is using xref:dataweave-flow-control.adoc#control_flow_if_else[if-else] and xref:dataweave-flow-control.adoc#control_flow_else_if[else-if].

The following example sets `United States` as the default value for `userLocation` if it is not present, or its value is `null`, in the JSON input message:

[source,DataWeave, linenums]
----
%dw 2.0
output application/json
---
if (payload.location != null) {
	"userLocation" : payload.location
} else {
	"userLocation" : "United States"
}
----

If the Mule application receives a JSON message with a value set for the `location` field, then the DataWeave  transformation matches field `location` to `userLocation`.

Example: Input JSON
[source,json, linenums]
----
{
  "location": "Argentina"
}
----

Example: Transformation Output
[source,JSON,linenums]
----
{
  "userLocation": "Argentina"
}
----

However, if the Mule application receives a JSON message without the expected field, or the field is `null`, then the transformation uses the configured default value for field `userLocation`.

Example: Input JSON
[source,json, linenums]
----
{}
----

Example: Output JSON
[source,JSON,linenums]
----
{
  "userLocation": "United States"
}
----

== Example of Using Matching Patterns to Set Default Values

In xref:dataweave-pattern-matching.adoc[pattern-matching scripts], DataWeave
`case` statements end with an `else` expression that can serve as the default
to return if all preceding `case` expressions return `false`.

Example: DataWeave Script
[source,DataWeave, linenums]
----
%dw 2.0
var myVar = "someString"
output application/json
---
myVar match {
    case myVarOne if (myVar == "some") -> ("some" ++ "is myVar")
    case myVarOne if (myVar == "strings") -> ("strings" ++ "is myVar")
    else -> myVar ++ " is myVar"
}
----

Example: Transformation Output
[source,JSON,linenums]
----
"someString is myVar"
----

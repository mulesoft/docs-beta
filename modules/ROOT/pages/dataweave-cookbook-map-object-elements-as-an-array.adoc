= Map Object Elements as an Array
ifndef::env-site,env-github[]
include::_attributes.adoc[]
endif::[]
:keywords: studio, anypoint, transform, transformer, format, aggregate, rename, split, filter convert, json, metadata, dataweave, data weave, datamapper, dwl, dfl, dw, output structure, input structure, map, mapping, as

This DataWeave example uses the DataWeave `map` function to iterate through the object elements that match the key `book`. The input also includes the key `magazine`, which is ignored.
//LINK TO DW 1.0 EXAMPLES:
include::partial$dataweave1-links.adoc[tag=dataweave1Examples]

The example uses these DataWeave functions:

* The multi-value selector `*book` to return an array with the elements that match the key `"book"`.
* `map` to go through each object in the array that's returned by the multi-value selector.


.DataWeave
[source,dataweave,linenums]
----
%dw 2.0
var myInputExample = {
  "inventory": {
      "book" : {
        "category": "cooking",
        "title": "Everyday Italian",
        "author": "Giada De Laurentiis",
        "year": "2005",
        "price": "30.00"
      },
      "book" :{
        "category": "children",
        "title": "Harry Potter",
        "author": "J K. Rowling",
        "year": "2005",
        "price": "29.99"
      },
      "book" :{
        "category": "web",
        "title": "Learning XML",
        "author": "Erik T. Ray",
        "year": "2003",
        "price": "39.95"
      },
      "magazine" :{
        "category": "web",
        "title": "Wired Magazine",
        "edition": "03-2017",
        "price": "15.95"
      },
        "magazine" :{
        "category": "business",
        "title": "Time Magazine",
        "edition": "04-2017",
        "price": "17.95"
      }
   }
}
output application/json
---
items: myInputExample.inventory.*book map (item, index) -> {
      "theType": "book",
      "theID": index,
      "theCategory": item.category,
      "theTitle": item.title,
      "theAuthor": item.author,
      "theYear": item.year,
      "thePrice": item.price as Number
}
----

.Output
[source,json,linenums]
----
{
  "items": [
    {
      "theType": "book",
      "theID": 0,
      "theCategory": "cooking",
      "theTitle": "Everyday Italian",
      "theAuthor": "Giada De Laurentiis",
      "theYear": "2005",
      "thePrice": 30.00
    },
    {
      "theType": "book",
      "theID": 1,
      "theCategory": "children",
      "theTitle": "Harry Potter",
      "theAuthor": "J K. Rowling",
      "theYear": "2005",
      "thePrice": 29.99
    },
    {
      "theType": "book",
      "theID": 2,
      "theCategory": "web",
      "theTitle": "Learning XML",
      "theAuthor": "Erik T. Ray",
      "theYear": "2003",
      "thePrice": 39.95
    }
  ]
}
----





== Related Examples

* xref:dataweave-cookbook-map.adoc[Map Data with DataWeave]

* xref:dataweave-cookbook-map-an-object.adoc[Map an Object]

* xref:dataweave-cookbook-exclude-field.adoc[Exclude Field]

* xref:dataweave-cookbook-rename-keys.adoc[Rename Keys]



== See Also

* xref:dataweave-types.adoc[DataWeave Types]

* xref:dataweave-selectors.adoc[DataWeave Selectors]

* xref:dataweave-cookbook.adoc[DataWeave Cookbook]
